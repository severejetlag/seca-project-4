buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.avast.gradle:gradle-docker-compose-plugin:0.6.17"
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker-compose'
apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom 'org.springframework.cloud:spring-cloud-netflix:1.4.0.RELEASE'
	}
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compileOnly 'org.projectlombok:lombok'
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile 'io.rest-assured:rest-assured:3.0.6'
	testCompile 'org.seleniumhq.selenium:selenium-java:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-api:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-remote-driver:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:3.9.1'
	testCompile 'org.seleniumhq.selenium:selenium-support:3.9.1'
	testCompile 'com.codeborne:selenide:4.10.01'
	runtime 'org.postgresql:postgresql'
	runtime 'org.springframework.boot:spring-boot-devtools'
	compile 'org.springframework.cloud:spring-cloud-starter-eureka'
}

task waitForAPIs(type: Exec) {
	commandLine './wait-for-users-api.sh','http://localhost:8080/users'
}

test {
	dependsOn cleanTest
	dependsOn waitForAPIs

	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		displayGranularity 2
	}
}

task runUsersAPITests(type: Exec) {
	workingDir './users-api'
	commandLine './gradlew', 'test'
}

task allTests(type: Test) {
	dependsOn cleanTest
	dependsOn waitForAPIs
	dependsOn runUsersAPITests

	testLogging {
		exceptionFormat "full"
		events "skipped", "passed", "failed"
		displayGranularity 2
	}
}

dockerCompose {
	useComposeFiles = ['docker-compose.test.yml']
}
dockerCompose.isRequiredBy(test)
dockerCompose.isRequiredBy(allTests)